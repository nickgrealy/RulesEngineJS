
plugins {
  id "com.eriwen.gradle.js" version "1.12.1"
}

repositories {
    mavenCentral()
}

defaultTasks 'clean', 'gzipJs', 'jshint', 'jsdoc'

ext {
	jsSrcDir = 'src/main/js'
	jsDistDir = 'src/dist/js'
	jsDocsDir = "${projectDir}/docs"
	jsDistPrefix = 'RulesEngine-dist'
}

javascript.source {
    prod {
        js {
            srcDir file(jsSrcDir)
            include "*.js"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}

task clean << {
	ant.delete(dir: buildDir)
	ant.delete(dir: jsDistDir)
	ant.delete(dir: jsDocsDir)
}

combineJs {
    source = javascript.source.prod.js.files
    dest = file("${jsDistDir}/${jsDistPrefix}.js")
}

minifyJs {
    source = combineJs
    dest = file("${jsDistDir}/${jsDistPrefix}.min.js")
    closure {
        warningLevel = 'QUIET'
    }
}

gzipJs {
    source = minifyJs
    dest = file("${jsDistDir}/${jsDistPrefix}.min.gz.js")
}

jshint {
    tasks.jshint.source = javascript.source.prod.js.files
    tasks.jshint.dest = file("${buildDir}/jshint.out")
}

jsdoc {
    tasks.jsdoc.source = javascript.source.prod.js.files
    tasks.jsdoc.destinationDir = file(jsDocsDir)
}
